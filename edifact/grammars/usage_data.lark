file: file_header licence_usage_transaction+ file_trailer

_m{x}: _BACKSLASH x

_o{x}: _BACKSLASH [x]

file_header: _LINE_NUMBER \
    _m{"fileHeader"} \
    _m{SOURCE_SYSTEM} \
    _m{DESTINATION_SYSTEM} \
    _m{DATA_ID} \
    _m{CREATION_DATE_TIME} \
    _m{RUN_NUMBER} \
    _o{RESET_RUN_NUM} \
    _TRAIL

file_trailer: _LINE_NUMBER \
    _m{"fileTrailer"} \
    _m{LICENCE_USAGE_COUNT} \
    _TRAIL?

licence_usage_transaction: licence_usage_transaction_header licence_line+ licence_usage_transaction_trailer

licence_usage_transaction_header: _LINE_NUMBER \
    _m{"licenceUsage"} \
    _m{TRANSACTION_REF} \
    _m{ACTION} \
    _m{LICENCE_REF} \
    _m{LICENCE_STATUS} \
    _o{COMPLETION_DATE} \
    _TRAIL

licence_usage_transaction_trailer: _LINE_NUMBER \
    _m{"end"} \
    _m{"licenceUsage"} \
    _m{RECORD_COUNT} \
    _TRAIL

licence_line: licence_line_header licence_usage* licence_line_trailer

licence_line_header: _LINE_NUMBER \
    _m{"line"} \
    _m{LINE_NUM} \
    _m{QUANTITY_USED} \
    _m{VALUE_USED} \
    _o{CURRENCY} \
    _TRAIL

licence_line_trailer: _LINE_NUMBER \
    _m{"end"} \
    _m{"line"} \
    _m{RECORD_COUNT} \
    _TRAIL

licence_usage: _LINE_NUMBER \
    _m{"usage"} \
    _m{USAGE_TYPE} \
    _m{DECLARATION_UCR} \
    _m{DECLARATION_PART_NUM} \
    _m{CONTROL_DATE} \
    _m{QUANTITY_USED} \
    _o{VALUE_USED} \
    _o{CURRENCY} \
    _o{TRADER_ID} \
    _o{CLAIM_REF} \
    _o{ORIGIN_COUNTRY} \
    _o{CUSTOMS_MIC} \
    _o{CUSTOMS_MESSAGE} \
    _optional_licence_usage? \
    _TRAIL

_optional_licence_usage: _o{CONSIGNEE_NAME} \
    _o{DECLARATION_MRN} \
    _o{DEPARTURE_ICS}

_LINE_NUMBER: NUMBER
DATE: DIGIT ~ 8
TIME: DIGIT ~ 4
TIMESTAMP: DATE TIME
RUN_NUMBER: NUMBER
LICENCE_USAGE_COUNT: NUMBER
_BACKSLASH: "\\"
TRANSACTION_REF: "LU" RUN_NUMBER "/" TRANSACTION_SEQUENCE_NUMBER
TRANSACTION_SEQUENCE_NUMBER: NUMBER
LICENCE_STATUS: "C" | "E" | "O" | "S" | "D"
COMPLETION_DATE: DATE
RECORD_COUNT: NUMBER
SYMBOL: "/" | "-"
LICENCE_REF: (LETTER | DIGIT | SYMBOL)+
LINE_NUM: NUMBER
CURRENCY: LETTER ~ 3
QUANTITY_USED: NUMBER | FLOAT
VALUE_USED: NUMBER | FLOAT
USAGE_TYPE: "A" | "C" | "L" | "M" | "O"
DECLARATION_UCR: (LETTER | DIGIT | SYMBOL) ~ 1..35
DECLARATION_PART_NUM: (LETTER | DIGIT | SYMBOL) ~ 1..4
CONTROL_DATE: DATE
TRADER_ID: (LETTER | DIGIT | SYMBOL) ~ 1..17
COUNTRY_CODE: LETTER ~ 2
ORIGIN_COUNTRY: COUNTRY_CODE
CUSTOMS_MIC: (LETTER | DIGIT) ~ 4
CUSTOMS_MESSAGE: WORD+
CLAIM_REF: WORD
CONSIGNEE_NAME: WORD+
DECLARATION_MRN: (LETTER | DIGIT) ~ 18
DEPARTURE_ICS: DIGIT ~ 2
ACTION: "insert"
SOURCE_SYSTEM: "CHIEF"
DESTINATION_SYSTEM: "SPIRE"
DATA_ID: "usageData"
CREATION_DATE_TIME: TIMESTAMP
RESET_RUN_NUM: "Y" | "N"

%import common.LETTER
%import common.INT -> NUMBER
%import common.FLOAT
%import common.DIGIT
%import common.NEWLINE -> _TRAIL
%import common.WS
%import common.WORD
