file: file_header licence_usage_transaction+ file_trailer
file_header: _LINE_NUMBER \
    _BACKSLASH \
    "fileHeader" \
    _BACKSLASH \
    "CHIEF" \
    _BACKSLASH \
    "SPIRE" \
    _BACKSLASH \
    "usageData" \
    _BACKSLASH \
    TIMESTAMP \
    _BACKSLASH \
    RUN_NUMBER \
    _BACKSLASH
file_trailer: _LINE_NUMBER \
    _BACKSLASH \
    "fileTrailer" \
    _BACKSLASH \
    LICENCE_USAGE_COUNT
licence_usage_transaction: licence_usage_transaction_header licence_line+ licence_usage_transaction_trailer
licence_usage_transaction_header: _LINE_NUMBER \
    _BACKSLASH \
    "licenceUsage" \
    _BACKSLASH \
    TRANSACTION_REF \
    _BACKSLASH \
    "insert" \
    _BACKSLASH \
    LICENCE_REF \
    _BACKSLASH \
    LICENCE_STATUS \
    _BACKSLASH
licence_usage_transaction_trailer: _LINE_NUMBER \
    _BACKSLASH \
    "end" \
    _BACKSLASH \
    "licenceUsage" \
    _BACKSLASH \
    RECORD_COUNT
licence_line: licence_line_header licence_usage* licence_line_trailer
licence_line_header: _LINE_NUMBER \
    _BACKSLASH \
    "line" \
    _BACKSLASH \
    LINE_NUM \
    _BACKSLASH \
    QUANTITY_USED \
    _BACKSLASH \
    VALUE_USED \
    _BACKSLASH \
    CURRENCY?
licence_line_trailer: _LINE_NUMBER \
    _BACKSLASH \
    "end" \
    _BACKSLASH \
    "line" \
    _BACKSLASH \
    RECORD_COUNT
licence_usage: _LINE_NUMBER \
    _BACKSLASH \
    "usage" \
    _BACKSLASH \
    USAGE_TYPE \
    _BACKSLASH \
    DECLARATION_UCR \
    _BACKSLASH \
    DECLARATION_PART_NUM \
    _BACKSLASH \
    CONTROL_DATE \
    _BACKSLASH \
    QUANTITY_USED \
    _BACKSLASH \
    VALUE_USED \
    _BACKSLASH \
    CURRENCY? \
    _BACKSLASH \
    TRADER_ID \
    _BACKSLASH \
    CLAIM_REF? \
    _BACKSLASH \
    ORIGIN_COUNTRY? \
    _BACKSLASH \
    CUSTOMS_MIC? \
    _BACKSLASH \
    CUSTOMS_MESSAGE? \
    _BACKSLASH \
    CONSIGNEE_NAME? \
    _BACKSLASH \
    DECLARATION_MRN? \
    _BACKSLASH \
    DEPARTURE_ICS?

_LINE_NUMBER: NUMBER
DATE: DIGIT ~ 8
TIME: DIGIT ~ 4
TIMESTAMP: DATE TIME
RUN_NUMBER: NUMBER
LICENCE_USAGE_COUNT: NUMBER
_BACKSLASH: "\\"
TRANSACTION_REF: "LU" RUN_NUMBER "/" TRANSACTION_SEQUENCE_NUMBER
TRANSACTION_SEQUENCE_NUMBER: NUMBER
LICENCE_STATUS: ("C"|"E"|"O"|"S"|"D")
COMPLETION_DATE: TIMESTAMP
RECORD_COUNT: NUMBER
LICENCE_REF: (LETTER | DIGIT | "/" | "-")+
LINE_NUM: NUMBER
CURRENCY: LETTER ~ 3
QUANTITY_USED: (NUMBER|FLOAT)
VALUE_USED: (NUMBER|FLOAT)
USAGE_TYPE: ("A"|"C"|"L"|"M"|"O")
DECLARATION_UCR: (LETTER | DIGIT | "/" | "-")+
DECLARATION_PART_NUM: (LETTER | DIGIT | "/" | "-") ~ 1..4
CONTROL_DATE: DATE
TRADER_ID: (LETTER | DIGIT | "/" | "-") ~ 1..17
COUNTRY_CODE: LETTER ~ 2
ORIGIN_COUNTRY: COUNTRY_CODE
CUSTOMS_MIC: (LETTER | DIGIT) ~ 4
CUSTOMS_MESSAGE: WORD+
CLAIM_REF: WORD
CONSIGNEE_NAME: WORD+
DECLARATION_MRN: (LETTER | DIGIT) ~ 18
DEPARTURE_ICS: DIGIT ~ 2

%import common.LETTER
%import common.INT -> NUMBER
%import common.FLOAT
%import common.DIGIT
%import common.WORD
%import common.WS
%ignore WS
